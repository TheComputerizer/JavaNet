plugins {
    id 'idea'
    id 'java-library'
    id 'io.github.goooler.shadow' version '8.+'
}

group = project_package
version = project_version

java {
    toolchain.languageVersion = JavaLanguageVersion.of(23)
}

compileJava {
    options.deprecation = false //We don't need to log all of the deprecated stuff when compiling
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
        inheritOutputDirs = true
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    setArchiveBaseName project_id
    manifest {
        attributes([
                'Implementation-Version': project_version,
                'Main-Class': "${project_package}.JavaNet"
        ])
    }
    finalizedBy 'shadowJar'
}

void addRepoFilter(MavenArtifactRepository repo, String filter) {
    def groups = properties["${filter}_content"].toString().split(';')
    repo.content {
        groups.each {
            includeGroup it
        }
    }
}

repositories {
    mavenCentral() {
        addRepoFilter it,'central'
    }
}

def dependencyOf(String name) {
    def dep_group = properties["${name}_group"]
    def dep_name = properties["${name}_name"]
    def dep_version = properties["${name}_version"]
    return "$dep_group:$dep_name:$dep_version".toString()
}

dependencies {
    implementation dependencyOf('logback')
    implementation dependencyOf('nd4j')
    implementation dependencyOf('dl4j')

    def lombok = dependencyOf 'lombok'
    annotationProcessor lombok
    compileOnly lombok
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}